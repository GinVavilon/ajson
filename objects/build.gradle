final ARTIFACT_ID = 'objects'
final NAME = 'AJson-Objects'
apply plugin: 'com.android.library'
apply plugin: 'android-eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    
    testCompile 'junit:junit:4.12'
}

androidEclipse{

   manifest = main
   pluginType = ANDMORE
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task jar(type: Jar, dependsOn: 'bundleRelease') {
    from zipTree("$buildDir/intermediates/bundles/default/classes.jar")
}

configurations {
    mavenDeployer
}

artifacts {
 	archives jar
    archives javadocsJar
    archives sourcesJar
}

publishing {
    publications {
            AjsonPublication(MavenPublication) {
                    
                    groupId GROUP_ID
                    artifactId PREFIX_ARTIFACT + ARTIFACT_ID
                    version VERSION
                    artifact jar
                    artifact bundleRelease
                    artifact sourcesJar
                    artifact javadocsJar
                    updatePom(pom, NAME, DESCRIPTION)
            }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['AjsonPublication']
    pkg {
            repo = 'maven'
            name = REPOSITOTY_NAME
            description = DESCRIPTION
            userOrg = user
            licenses = LICENSES
            vcsUrl = VCS_URL
            labels = ['android',  'json']
            publicDownloadNumbers = PUBLISH
            publish = PUBLISH
            version {
                    name = VERSION
                    desc = gitTag?.fullMessage
                    vcsTag = gitTag?.tagName
                    released = gitTag?.taggerIdent?.when?.format("yyyy-MM-dd'T'HH:mm:ss.SSSZZ")
            }
    }
}
